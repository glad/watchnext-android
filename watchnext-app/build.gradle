apply plugin: 'com.android.application'

android {
    Properties settingsProperties = new Properties()
    settingsProperties.load(project.file("settings.properties").newDataInputStream())

    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.glad.watchnext"

        minSdkVersion 21
        targetSdkVersion 28

        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "THEMOVIEDB_API_BASE_URL", '"https://api.themoviedb.org/3/"'
        buildConfigField "String", "THEMOVIEDB_IMAGES_BASE_URL", '"https://image.tmdb.org/t/p/"'
        buildConfigField "String", "THEMOVIEDB_API_KEY", '"' + settingsProperties.getProperty("themoviedb.api_key", "") + '"'

        buildConfigField "String", "DEFAULT_IMAGE_QUALITY", '"MEDIUM"'
        buildConfigField "long", "DEFAULT_MAX_CACHE_SIZE_BYTES", '5L * 1048576L /* 5MB */'
        buildConfigField "long", "DEFAULT_MAX_CACHE_AGE_MS", '5L * 60000L /* 5 min. */'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation project(':watchnext-data')
    implementation project(':watchnext-domain')
    implementation project(':watchnext-platform')

    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    implementation 'com.google.dagger:dagger:2.23.2'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.23.2'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.6'

    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
    implementation "com.github.bumptech.glide:okhttp3-integration:4.9.0"

    implementation 'com.squareup.picasso:picasso:2.71828'

    implementation 'com.android.support:exifinterface:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:preference-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.27.0'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
